# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem              1.0
PortGroup               compiler_blacklist_versions 1.0
PortGroup               active_variants 1.1

name                    libcxx
epoch                   1
version                 9.0.1
revision                0
categories              lang
platforms               darwin
license                 MIT NCSA
maintainers             {jeremyhu @jeremyhu} {kencu @kencu}
description             libc++ is a new implementation of the C++ standard library with support for C++11 and portions of C++14.
long_description        ${description} \
                        Because objects cannot be passed between different versions of the C++ runtime, this port must \
                        replace the host version in order to be used.  On Snow Leopard and earlier, this is done \
                        automatically because there is no existing host version of this library.  On Lion and later, \
                        users must build the port with +replacemnt_libcxx and install the root manually if they wish \
                        to replace the existing host implementation.

homepage                https://libcxx.llvm.org/

#master_sites            https://releases.llvm.org/${version}/
master_sites            https://github.com/llvm/llvm-project/releases/download/llvmorg-9.0.1/
dist_subdir             llvm

if {${os.platform} eq "darwin" && ${os.major} < 11 && ${configure.cxx_stdlib} eq "libc++"} {
    # Bootstrap problem, libcxx is indirectly used by the normal xz port.
    depends_extract     port:xz-bootstrap
    depends_skip_archcheck-append   xz-bootstrap
    extract.suffix      .tar.xz
    extract.cmd         ${prefix}/libexec/libcxx-bootstrap/bin/xz
    # And having the stdlib set to libc++ on 10.6 causes a
    # macports-clang compiler to be selected.
    configure.cxx_stdlib
} else {
    use_xz              yes
}

set libcxxabi_distname  libcxxabi-${version}.src
set libcxx_distname     libcxx-${version}.src

distfiles               ${libcxxabi_distname}${extract.suffix} \
                        ${libcxx_distname}${extract.suffix}

checksums               libcxxabi-9.0.1.src.tar.xz \
                        rmd160  8f1529d722a6ffe44c44290dbea5a8a9ca2acea9 \
                        sha256  e8f978aa4cfae2d7a0b4d89275637078557cca74b35c31b7283d4786948a8aac \
                        size    552088 \
                        libcxx-9.0.1.src.tar.xz \
                        rmd160  b85b557e1fa372f2f3eb854c13ca103abb671d66 \
                        sha256  0981ff11b862f4f179a13576ab0a2f5530f46bd3b6b4a90f568ccc6a62914b34 \
                        size    1813356

use_configure no

set roots_path ${prefix}/var/system_roots/
set root_name  ${name}-${version}-${revision}

set libcxxabi_worksrcpath   [file join ${workpath} ${libcxxabi_distname}]
set libcxx_worksrcpath      [file join ${workpath} ${libcxx_distname}]

platform darwin {
    if {${os.major} < 9} {
        known_fail      yes
        pre-fetch {
            ui_error "${name} is not supported on Tiger or earlier."
            error "unsupported platform"
        }
    }

    if {${os.major} < 11} {
        post-activate {
            set dirs /
            if {${os.subplatform} eq "macosx"} {
                lappend dirs ${developer_dir}/SDKs/MacOSX[join [lrange [split ${macosx_version} .] 0 1] .].sdk
            }
            foreach d ${dirs} {
                system -W ${d} "tar xzf ${roots_path}/${root_name}.tgz"
            }
        }
    }
}

variant replacemnt_libcxx description {EXPERTS ONLY: Build a replacement libcxxabi and libcxx even if it is already part of the base OS.} {}

if {${os.major} < 11 || [variant_isset replacemnt_libcxx]} {

    supported_archs i386 x86_64

    if {${build_arch} in "i386 x86_64"} {
        variant universal {}
        default_variants +universal
        configure.universal_archs i386 x86_64
    }

    if {${os.major} < 11} {
        variant emulated_tls description {build libcxxabi and libcxx with support for emulated thread_local storage} {}

        # only selected clang versions support emulated_tls, and the emulated_tls variant
        # needs to be enabled to build libcxx with emulated_tls support
        foreach clang_c ${compiler.fallback} {
            if {[string match macports-clang-* $clang_c]} {
                # Extract version number from macports clang compiler name
                set clang_v [lindex [split $clang_c -] 2]
                if { ${clang_v} >= 5.0 } {
                    if {[file exists ${prefix}/bin/clang-mp-${clang_v}]} {
                        if {[active_variants clang-${clang_v} emulated_tls]} {
                            ui_debug "Adding ${clang_c} to compiler whitelist"
                            default_variants-append +emulated_tls
                            compiler.whitelist-prepend ${clang_c}
                        }
                    }
                }
            }
        }
    }

    compiler.blacklist *gcc* {clang < 500}

    # clang 3.5 and newer are conditionally blacklisted to prevent dependency cycles
    foreach clang_c ${compiler.fallback} {
        if {[string match macports-clang-* $clang_c]} {
            # Extract version number from macports clang compiler name
            set clang_v [lindex [split $clang_c -] 2]
            if { ${clang_v} >= 3.5 } {
                if {![file exists ${prefix}/bin/clang-mp-${clang_v}]} {
                    ui_debug "Adding ${clang_c} to compiler blacklist"
                    compiler.blacklist-append ${clang_c}
                }
            }
        }
    }

    post-extract {
        # delete cxa_noexception.cpp as we're not using the cmake build system
        # cmake would choose either cxa_exceptions or cxa_noexceptions
        # but the buildit script just builds every file in the src directory and you can't have both
        delete ${libcxxabi_worksrcpath}/src/cxa_noexception.cpp

        if {![variant_isset emulated_tls]} {
            # cmake doesn't include this for darwin builds, so nuke it here as well
            # however, this file is essential for emulated-tls to function
            delete ${libcxxabi_worksrcpath}/src/cxa_thread_atexit.cpp
        }

        file mkdir ${workpath}/build
        file copy ${filespath}/Makefile ${workpath}/build
    }

    patch.dir ${workpath}
    patch.pre_args -p0

    # patches for libc++abi buildit
    patchfiles-append \
        0000-libcxxabi-restore-buildit.patch \
        0001-libcxxabi-Update-buildit-script-to-read-EXTRA_LDFLAGS-from-the.patch \
        0002-libcxxabi-Define-_LIBCPP_BUILDING_LIBRARY-during-build-to-supp.patch \
        0003-libcxxabi-buildit-Use-undefined-dynamic_lookup-when-linking.patch    \
        0004-libcxxabi-buildit-add-nostdinc++.diff

    if {[variant_isset emulated_tls]} {
      # add emutls.c support for cxa_thread_atexit directly into libcxxabi
      # these files were taken from clang-7.0's compiler-rt subproject
      patchfiles-append \
        0005-libcxxabi-add-emutls.patch                                           \
        0006-libcxxabi-buildit-build-emutls.patch                                 \
        0007-libcxxabi-include-expose-cxa-thread-atexit.patch                     \
        0008-libcxxabi-cxa-thread-atexit-no-weak-atexit-impl.patch
    }

    # restore libc++ buildit and patches for libc++ buildit
    patchfiles-append \
        1001-Revert-Remove-the-buildit-and-testit-scripts-they-ha.patch          \
        1002-libcxx-buildit-build-fix-for-Leopard.patch \
        1003-libcxx-buildit-Set-compatibility-version-to-RC_ProjectSourc.patch \
        1004-libcxx-MacPorts-Use-LIBCXXABI_PATH-for-path-to-build-time-l.patch \
        1005-libcxx-buildit-Fix-CFLAGS-for-Leopard-and-Snow-Leopard-to-i.patch

    # patches for libc++ include files
    patchfiles-append \
        2002-Fix-missing-long-long-math-prototypes-when-using-the.patch    \
        2003-implement-atomic-using-mutex-lock_guard-for-64b-ops-.patch

# these files have changed
#         2001-Fix-local-and-iterator-when-building-with-Lion-and-n.patch    \
#        2004-libcxx-includes-Fix-up-thread-constexpr-for-GCC-where-the-PPC-port-cant-do-longlong-constexpr.patch

    post-patch {
        system "chmod 755 ${libcxx_worksrcpath}/lib/buildit"
        system "chmod 755 ${libcxxabi_worksrcpath}/lib/buildit"
    }

    build.dir ${workpath}/build
    build.env-append \
        CC=${configure.cc} \
        CXX=${configure.cxx} \
        RC_XBS=1 \
        RC_ProjectSourceVersion=${version} \
        "RC_CFLAGS=[get_canonical_archflags] -I${libcxxabi_worksrcpath}/include -I${libcxx_worksrcpath}/include ${configure.optflags}" \
        TRIPLE=-apple-darwin${os.major}
    build.args-append \
        LIBCXXABI_PATH=${libcxxabi_worksrcpath} \
        LIBCXX_PATH=${libcxx_worksrcpath} \
        LIBCXXABI_DYLIB_PATH=${build.dir}/libc++abi.dylib

    # We want to use the host's libc++ everywhere because we want to ensure
    # that only *ONE* copy of this library is in any process's address space.
    # On Snow Leopard and earlier, this port provides this library in /usr for
    # binary compatibility with Lion and later.
    #
    # On newer OS version, this port does not install the content to /usr and
    # just leaves it to expert users who may want to replace their system
    # runtime.

    destroot {
        xinstall -m 755 -d ${destroot}${roots_path}/${root_name}/usr/lib

        xinstall -m 755 ${libcxxabi_worksrcpath}/lib/libc++abi.dylib ${destroot}${roots_path}/${root_name}/usr/lib

        xinstall -m 755 ${libcxx_worksrcpath}/lib/libc++.1.dylib ${destroot}${roots_path}/${root_name}/usr/lib
        ln -s libc++.1.dylib ${destroot}${roots_path}/${root_name}/usr/lib/libc++.dylib

        xinstall -m 755 -d ${destroot}${roots_path}/${root_name}/usr/include
        xinstall -m 755 ${libcxxabi_worksrcpath}/include/__cxxabi_config.h ${destroot}${roots_path}/${root_name}/usr/include
        xinstall -m 755 ${libcxxabi_worksrcpath}/include/cxxabi.h ${destroot}${roots_path}/${root_name}/usr/include

        system -W ${destroot}${roots_path}/${root_name} "tar czf ../${root_name}.tgz ."
        delete ${destroot}${roots_path}/${root_name}
    }
} else {
    supported_archs noarch
    distfiles
    build {}
    destroot {
        xinstall -d ${destroot}${prefix}/share/doc/${name}
        system "echo ${name} is an empty port on this OS version because the functionality is already provided by the OS. > ${destroot}${prefix}/share/doc/${name}/README.txt"
    }
}

livecheck.type none
